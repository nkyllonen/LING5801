!######################################################################
!#
!# FINITE-STATE TRANSDUCER FOR ENGLISH REGULAR VERB PARSING/GENERATION:
!#
!##############################################################

define Con [b|c|d|f|g|h|j|k|l|m|n|p|q|r|s|t|v|x|w|z] ;

define Vowel [a|e|i|o|u] ;

define DoublingCs [b|c|g|m|n|p|r|t|v|d] ;

!######################################################################
!# BUILD LEXICAL TRANSDUCER


## Verb class sublexicons:

define RegVerbStem [ {walk} |
       		     {try} |
		     {fry} | 
                     {impeach} |
		     {goose} |
		     {sway} |
		     {beg} |
                     {tip} |
		     {tab} |
		     {picnic} |
		     {hem} |
		     {pin} | 
                     {spar} |
		     {pit} |
		     {rev} |
		     {bud} ] ;



!## Morphological features: noun and number tags:

define Verb %+V:0 ;

define ThirdSg  %+Pres%+3sg .x. %^ s %# ;

define PresPart %+PresPart .x. %^ i n g %# ;

define PastPart %+PastPart .x. %^ e d %# ;

define Past %+Past .x. %^ e d %# ;



## Lexical transducer:

define VerbLex  [ RegVerbStem Verb [ ThirdSg | PresPart | PastPart | Past 
                                             | %+Plain:0  ] ] ; 

					     

!######################################################################
!# RULE TRANSDUCERS:
#
# E-insertion

define eInsertion [..] -> e || [ s | z | x | s h | c h ] %^ _ s ;
 
# yReplacment

define yReplacement [ y -> i e || Con _ %^ [ s | e d ]  ] ;

# eDeletion

define eDeletion [ e -> 0 || _ %^ Vowel ] ;

# cDoubling

define cDoubling [ b -> b b, c -> c k, g -> g g, 
		   m -> m m, n -> n n, p -> p p,
                   r -> r r, t -> t t, v -> v v, d -> d d || Vowel _ %^ [i|e] ] ;

!define cDoubling [ DoublingCs -> ... ... || Vowel _ %^ [i|e] ] ;

# Remove morpheme and word boundary markers.

define StemBoundary %^ -> 0 ;
define WordBoundary %# -> 0 ;


# Compose rule transducers: 

define RuleT eInsertion .o. yReplacement .o. eDeletion  .o. cDoubling
                        .o. StemBoundary .o. WordBoundary ;




!######################################################################
!# Compose lexical and rule-transducers; push onto stack;

define MorphVerbParser VerbLex .o. RuleT ;
push MorphVerbParser
